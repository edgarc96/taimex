<pre>

PicLan-IP/BASIC ^ ^
*
OPEN 'XMLDATA' TO XMLDATAF ELSE STOP 201,'XMLDATA'
OPEN 'WO' TO WOF ELSE STOP 201,'WO'

PL_GETVAR WO_ID FROM 'WO_ID' ELSE WO_ID = ''
PL_GETVAR ACTION FROM 'ACTION' ELSE ACTION = ''

PL_ADD_HDR '"Content-Type: application/xml"'

CRLF = CHAR(13):CHAR(10)
XML.RESPONSE = '<?xml version="1.0" encoding="UTF-8"?>':CRLF
XML.RESPONSE = XML.RESPONSE:'<WorkOrder>':CRLF
XML.RESPONSE = XML.RESPONSE:'  <wo_id>':WO_ID:'</wo_id>':CRLF
XML.RESPONSE = XML.RESPONSE:'  <action>':ACTION:'</action>':CRLF

IF WO_ID = '' THEN
    XML.RESPONSE = XML.RESPONSE:'  <status>ERROR</status>':CRLF
    XML.RESPONSE = XML.RESPONSE:'  <message>No WO_ID provided</message>':CRLF
    GOTO FINISH
END

READ WO.RECORD FROM WOF, WO_ID ELSE
    XML.RESPONSE = XML.RESPONSE:'  <status>ERROR</status>':CRLF
    XML.RESPONSE = XML.RESPONSE:'  <message>Work Order not found: ':WO_ID:'</message>':CRLF
    GOTO FINISH
END

WO_DATE = WO.RECORD<1>
WO_TYPE = WO.RECORD<20>
NOTES = WO.RECORD<25>
ASSY_NO = WO.RECORD<32>
SCHEDULE_DATE = WO.RECORD<34>
ORD_QTY = WO.RECORD<35>
YIELD = WO.RECORD<36>
START_DATE = WO.RECORD<104>

* Extract schedule_qty from schedule_date field
SCHEDULE_QTY = ''
IF SCHEDULE_DATE <> '' THEN
    IF INDEX(SCHEDULE_DATE, '*', 1) > 0 THEN
        SCHEDULE_QTY = FIELD(SCHEDULE_DATE, '*', 2)
    END
END

* Convert dates to MM/DD/YYYY
WO_DATE_OUT = ''
IF WO_DATE <> '' AND WO_DATE > 0 THEN
    WO_DATE_OUT = OCONV(WO_DATE, 'D4/')
END

START_DATE_OUT = ''
IF START_DATE <> '' AND START_DATE > 0 THEN
    START_DATE_OUT = OCONV(START_DATE, 'D4/')
END

SCHEDULE_DATE_OUT = ''
IF SCHEDULE_DATE <> '' THEN
    SCHED_JULIAN = SCHEDULE_DATE
    IF INDEX(SCHED_JULIAN, '*', 1) > 0 THEN
        SCHED_JULIAN = FIELD(SCHED_JULIAN, '*', 1)
    END
    IF SCHED_JULIAN <> '' AND SCHED_JULIAN > 0 THEN
        SCHEDULE_DATE_OUT = OCONV(SCHED_JULIAN, 'D4/')
    END
END

IF NOTES <> '' THEN NOTES = CHANGE(NOTES, @VM, CHAR(10))

XML.RESPONSE = XML.RESPONSE:'  <status>SUCCESS</status>':CRLF
XML.RESPONSE = XML.RESPONSE:'  <message>Work Order retrieved successfully</message>':CRLF
XML.RESPONSE = XML.RESPONSE:'  <data>':CRLF
XML.RESPONSE = XML.RESPONSE:'    <wo_date>':WO_DATE_OUT:'</wo_date>':CRLF
XML.RESPONSE = XML.RESPONSE:'    <wo_type>':WO_TYPE:'</wo_type>':CRLF
XML.RESPONSE = XML.RESPONSE:'    <start_date>':START_DATE_OUT:'</start_date>':CRLF
XML.RESPONSE = XML.RESPONSE:'    <notes>':NOTES:'</notes>':CRLF
XML.RESPONSE = XML.RESPONSE:'    <assy_no>':ASSY_NO:'</assy_no>':CRLF
XML.RESPONSE = XML.RESPONSE:'    <schedule_date>':SCHEDULE_DATE_OUT:'</schedule_date>':CRLF
XML.RESPONSE = XML.RESPONSE:'    <schedule_qty>':SCHEDULE_QTY:'</schedule_qty>':CRLF
XML.RESPONSE = XML.RESPONSE:'    <yield>':YIELD:'</yield>':CRLF
XML.RESPONSE = XML.RESPONSE:'    <ord_qty>':ORD_QTY:'</ord_qty>':CRLF
XML.RESPONSE = XML.RESPONSE:'  </data>':CRLF

FINISH:
XML.RESPONSE = XML.RESPONSE:'</WorkOrder>':CRLF

WRITE XML.RESPONSE ON XMLDATAF,'PROCESS.XML'
ERR = ''
CALL PLW.PAGE('/XMLDATA/PROCESS.XML','',ERR)
RETURN
</pre>